# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=awesome
pkgname=$_pkgname-git
pkgver=4.3.r994.g74027e4f2
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua53' 'lua53-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm' 'libxfixes')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'xcb-util-errors: for pretty-printing of X11 errors'
            'librsvg: for displaying SVG files without scaling artifacts')
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')
_LUA_VER=5.3

_patches=()

source=("$_pkgname::git+file:///home/amariya/projects/awesome"
        "${_patches[@]}")

sha512sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe | sed 's/^v//;s/-/.r/;s/-/./g'
}

prepare() {
    mkdir -p build

    local file
    cd "$_pkgname"

    for file in "${_patches[@]}"; do
        if [[ "$file" == *.h ]]; then
            cp "$srcdir/$file" .
        elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
            echo -e "\e[1mApplying patch: $(basename "$file")\e[0m"
            patch -Np1 <"$srcdir/$(basename "$file")"
        fi
    done
}

build() {
    cd build
    cmake ../"$_pkgname" \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONFDIR=/etc \
        -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
        -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER} \
        -DLUA_EXECUTABLE=/usr/bin/lua${_LUA_VER}
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir"/"$_pkgname"/awesome.desktop \
        "$pkgdir/usr/share/xsessions/awesome.desktop"
}
