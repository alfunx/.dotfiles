# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=ranger
pkgname=$_pkgname-git
pkgver=1.9.2.4.g036d2f31
pkgrel=1
pkgdesc="A simple, vim-like file manager."
arch=('any')
url="http://savannah.nongnu.org/projects/ranger"
license=('GPL')
depends=('python') #python(>=2.6 or >=3.1)
makedepends=('git')
optdepends=('atool: for previews of archives'
            'file: for determining file types'
            'highlight: for syntax highlighting of code'
            'libcaca: for ASCII-art image previews'
            'mediainfo: for viewing information about media files'
            'perl-image-exiftool: for viewing information about media files'
            'poppler: for pdf previews'
            'python-chardet: in case of encoding detection problems'
            'sudo: to use the "run as root"-feature'
            'transmission-cli: for viewing bittorrent information'
            'w3m: for previews of images and html pages'
            'python-pillow: for previews of images (kitty)'
            'python2-pillow: for previews of images (kitty)')
provides=('ranger')
conflicts=('ranger')
source=("$_pkgname::git+file:///home/amariya/projects/ranger")
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --always | sed -e 's|-|.|g' -e '1s|^.||'
}

package() {
  cd "$_pkgname"
  python setup.py -q install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/${pkgname%-*}/LICENSE
}
