# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=awesome
pkgname=$_pkgname-luajit-git
pkgver=4.3.r391.gcaf9a2666
pkgrel=1
pkgdesc="Highly configurable framework window manager, built with luajit"
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'luajit' 'luajit-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-xrm' 'xcb-util-wm' 'libxfixes')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto' 'lua-penlight')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager')
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')

_patches=()

source=("$_pkgname::git+file:///home/amariya/projects/awesome"
        awesome.desktop
        awesomeksm.desktop)

sha512sums=('SKIP'
            'eb77306d73a366f10a3d02f8def119ac9cf96f1258c669bb3c36ba4007bb49e8cdd5180e2cdf22f0e6b90bae2affc8ee4b4e10671bcaa705b4c13e16fcf34fbe'
            '386cb90c8a5ef06856dc26484ad1cb8eb79f31b56200f14239c87b34cc5e36393442387882e4da0851a81b8ce068e878bc1ba208c828b3c63b03886d6d482e1a')

pkgver() {
    cd "$_pkgname"
    git describe | sed 's/^v//;s/-/.r/;s/-/./g'
}

prepare() {
    mkdir -p build

    cd "$_pkgname"

    sed -i 's/COMMAND lua\b/COMMAND luajit/' awesomeConfig.cmake tests/examples/CMakeLists.txt
    sed -i 's/LUA_COV_RUNNER lua\b/LUA_COV_RUNNER luajit/' tests/examples/CMakeLists.txt
}

build() {
    cd build
    cmake ../"$_pkgname" \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONFDIR=/etc \
        -DLUA_INCLUDE_DIR=/usr/include/luajit-2.0 \
        -DLUA_LIBRARY=/usr/lib/libluajit-5.1.so
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir"/"$_pkgname"/awesome.desktop \
        "$pkgdir/usr/share/xsessions/awesome.desktop"

    install -Dm644 "$srcdir"/awesomeksm.desktop \
        "$pkgdir/usr/share/apps/ksmserver/windowmanagers/awesome.desktop"
}
