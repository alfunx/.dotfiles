# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=awesome
pkgname=${_pkgname}-git
pkgver=4.2.261.gfb0ccc32
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua' 'lua-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager')
provides=('notification-daemon' "${_pkgname}")
conflicts=("${_pkgname}")
backup=('etc/xdg/awesome/rc.lua')

_patches=()

source=("$pkgname::git+file:///home/amariya/projects/awesome/awesome"
        "${_patches[@]}")

sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build

  local file
  cd "$pkgname"

  for file in "${_patches[@]}"; do
    if [[ "$file" == *.h ]]; then
      cp "$srcdir/$file" .
    elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
      echo -e "\e[1mApplying patch: $(basename "$file")\e[0m"
      patch -Np1 <"$srcdir/$(basename "$file")"
    fi
  done
}

build() {
  cd build
  cmake ../"$pkgname" \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua.so.5.3
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/"$pkgname"/awesome.desktop \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
}
